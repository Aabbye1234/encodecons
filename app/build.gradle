import javax.crypto.Cipher
import javax.crypto.spec.IvParameterSpec
import javax.crypto.spec.SecretKeySpec

apply plugin: 'com.android.application'

static String encrypt(String key, String input) {
    try {
        SecretKeySpec secretKeySpec = new SecretKeySpec(key.getBytes(), "AES")
        IvParameterSpec ivParameterSpec = new IvParameterSpec(key.getBytes())
        Cipher cipher = Cipher.getInstance("AES/CBC/PKCS5Padding")
        cipher.init(Cipher.ENCRYPT_MODE, secretKeySpec, ivParameterSpec)
        return cipher.doFinal(input.getBytes()).encodeBase64().toString()
    } catch (Exception e) {
        e.printStackTrace()
    }
    return null
}

def KEY = "000" + new Date().getTime()

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.aabbye1234.encodecons"
        minSdkVersion 15
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "KEY", "\"" + KEY.getBytes().encodeBase64().toString() + "\""
        buildConfigField "String", "STR_AES", "\"" + "AES".getBytes().encodeBase64().toString() + "\""
        buildConfigField "String", "STR_AES_CBC_PKCS5Padding", "\"" + "AES/CBC/PKCS5Padding".getBytes().encodeBase64().toString() + "\""
        buildConfigField "String", "STR_1", "\"" + encrypt(KEY, "这是个需要加密的字符串") + "\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])


}